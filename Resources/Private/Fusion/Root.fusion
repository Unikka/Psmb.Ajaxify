# TODO: Watch out, this is hardcoded to be used with Psmb.Ajaxify:Ajaxify
prototype(Psmb.Ajaxify:RenderPath) {
	@class = 'Psmb\\Ajaxify\\Fusion\\RenderPathImplementation'
}

# Use this object as a processor on any path
prototype(Psmb.Ajaxify:Ajaxify) < prototype(Neos.Fusion:Tag) {
	@if.disableProcessor = ${!request.arguments.ajaxPathKey}
	tagName = 'a'
	attributes.href = NodeUri {
		node = ${documentNode}
		additionalParams.ajaxPathKey = Psmb.Ajaxify:RenderPath
	}
	content = 'Loader'
}

prototype(Psmb.Ajaxify:Renderer) {
	@class = 'Psmb\\Ajaxify\\Fusion\\RendererImplementation'
	node = ${documentNode}
	pathKey = ${request.arguments.ajaxPathKey}
}

root.ajaxify {
	@position = 'start'
	condition = ${request.arguments.ajaxPathKey}
	renderer = Psmb.Ajaxify:Renderer
}

prototype(Neos.Fusion:GlobalCacheIdentifiers).ajaxPathKey = ${request.arguments.ajaxPathKey}
